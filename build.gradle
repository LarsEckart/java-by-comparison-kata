plugins {
    id 'java'
    id 'jacoco'
    id 'application'
    id 'info.solidsoft.pitest' version '1.15.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.json:json:20230618")

    testImplementation(platform("org.junit:junit-bom:5.10.0"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.assertj:assertj-core:3.24.2")
    testImplementation("com.approvaltests:approvaltests:21.0.0")
    testImplementation("com.larseckart:junit-tcr-extensions:0.0.3")
    testImplementation("org.mockito:mockito-core:5.6.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.5.0")
}

tasks.withType(JavaCompile) {
    options.compilerArgs.add("-Xlint:all")
    options.incremental = true
}


application {
    mainClass.set("com.javabycomparison.kata.main.StaticAnalysis")
}

jar {
    manifest {
        attributes "Main-Class": "com.javabycomparison.kata.main.StaticAnalysis"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

project.tasks.check.dependsOn("pitest")
pitest {
    junit5PluginVersion = '0.15'
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    targetClasses = ['com.javabycomparison.kata.*']
    pitestVersion = '1.7.0'
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}
